/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

buildscript {
    ext {
        publishedPluginVersion = rootProject.properties['pluginVersion']
        pluginVersion = rootProject.properties['pluginVersion']
        esVersion = project.properties['esVersion']
        pluginName = rootProject.properties['pluginName']
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
    	classpath 'org.elasticsearch.gradle:build-tools:' + esVersion
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://repo.gradle.org/gradle/libs-releases-local' }
}

group = 'org.elasticsearch.plugin'
version = pluginVersion + '_es' + esVersion
def pluginFullName = pluginName + '-' + version

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'license'

dependencies {
    compile 'org.elasticsearch:elasticsearch:' + esVersion
    compile 'org.elasticsearch.plugin:transport-netty4-client:' + esVersion
    compile 'org.elasticsearch.client:elasticsearch-rest-client:' + esVersion
     compile 'com.google.guava:guava:21.0'
    compile 'com.unboundid:unboundid-ldapsdk:3.2.0'
    compile 'com.jayway.jsonpath:json-path:2.2.0'
    compile 'net.minidev:json-smart:2.3'
    compile 'net.minidev:accessors-smart:1.2'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.json:json:20160810'
    compile ('org.reflections:reflections:0.9.11'){
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'dom4j', module: 'dom4j'
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'junit:junit:4.12'
    testCompile 'org.gradle:gradle-tooling-api:2.13'
    testRuntime 'org.gradle:gradle-tooling-api:2.13'
    testCompile('org.testcontainers:testcontainers:1.1.9')
    testRuntime('org.testcontainers:testcontainers:1.1.9')
}

license {
    header rootProject.file('ReadonlyRESTLicenseHeader.txt')
    skipExistingHeaders true
    useDefaultMappings = false
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum file: it.archivePath, algorithm: 'sha1'
    }
}

configurations {
    wagon
    distJars {
        extendsFrom runtime
        exclude group: 'org.elasticsearch'
        exclude group: 'lucene-core'
        exclude group: 'org.apache.logging.log4j'
        exclude group: 'lucene-analyzers-common'
        exclude group: 'org.apache.commons'
        exclude group: 'org.yaml'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }
}

task cleanOldData {
    doLast {
        delete 'build/tmp/' + pluginFullName
    }
}

task jarHellCheck(type: JavaExec) {
    outputs.upToDateWhen { false }
    main = "org.elasticsearch.bootstrap.JarHell"
    classpath = project.sourceSets.main.compileClasspath.filter { it.exists() }
}

task configureEsVersion() {
    doLast {
        if (project.hasProperty('esVersion')) {
            esVersion = project.properties['esVersion']
        }
    }
}

task resolvePluginDescriptorTemplate(type: Copy, dependsOn: configureEsVersion) {
    outputs.upToDateWhen { false }
    from 'plugin-metadata'
    into 'build/tmp/' + pluginFullName
    expand([
            'descriptor': [
                    'name'         : pluginName,
                    'pluginVersion': project.properties['pluginVersion'],
                    'esVersion'    : project.properties['esVersion']
            ]
    ])
}

task ror(type: Zip, dependsOn: [cleanOldData, jarHellCheck, jar, resolvePluginDescriptorTemplate]) {
    outputs.upToDateWhen { false }
    archivesBaseName = pluginName
    into('elasticsearch') {
        from configurations.distJars
        from 'build/libs/' + pluginFullName + '.jar'
        from 'build/tmp/' + pluginFullName
    }
}